ext {
    // Attention:please make sure only one module wanted is set true
    isBuildingApp = true
    isBuildingEntranceModule = false
    isBuildingMainModule = false
    isBuildingChatModule = false

    tmsResourcePrefixs = [
            app           : "",
            entranceModule: "em_",
            mainModule    : "mm_",
            commonModule  : "cm_",
            chatModule    : 'cm1_'
    ]

    tmsIdsConfigs = [
            app           : "cn.edu.zust.dmt.hsy.tms",
            entranceModule: "cn.edu.zust.dmt.hsy.tms.em",
            mainModule    : "cn.edu.zust.dmt.hsy.tms.main",
            chatModule    : "cn.edu.zust.dmt.hsy.tms.chat"
    ]

    tmsAndroidConfigs = [
            compileSdkVersion: 28,
            minSdkVersion    : 21,
            targetSdkVersion : 28,
            versionCode      : 1,
            versionName      : "1.0"
    ]

    tmsJavaConfigs = [
            sourceCompatibility: "8",
            targetCompatibility: "8"
    ]
}

static def addTmsShellDependencies(final DependencyHandler dependencyHandler) {
    dependencyHandler.add("implementation", dependencyHandler.project(path: ':entrance_module'))
    dependencyHandler.add("implementation", dependencyHandler.project(path: ':main_module'))
}

static def addTmsModuleDependencies(final DependencyHandler dependencyHandler) {
    dependencyHandler.add("implementation", dependencyHandler.project(path: ':common_module'))
    dependencyHandler.add("implementation", dependencyHandler.project(path: ':my_base_library'))
    dependencyHandler.add("implementation", dependencyHandler.project(path: ':my_annotations_library'))
    dependencyHandler.add("annotationProcessor", dependencyHandler.project(path: ':my_processors_library'))
}

ext.addTmsShellDependencies = this.&addTmsShellDependencies
ext.addTmsModuleDependencies = this.&addTmsModuleDependencies